/* Section 1: Options */
options {
   MULTI = true;
   VISITOR = true;
   JAVA_UNICODE_ESCAPE = true;
}

/* Section 2: Driver */
PARSER_BEGIN(HOPE2)

import java.io.FileInputStream;
import java.io.IOException;
import java.io.FileNotFoundException;
import java.io.BufferedWriter;
import java.io.FileWriter;

public class HOPE2 {

   public static SymbolTable st = new SymbolTable();

   public static void main(String args[]) {

      HOPE2 parser = null;
      String irFileName = "HOPE2.ll";

      if(args.length == 0) {
         System.out.println("Reading input...");
         parser = new HOPE2(System.in);
      }
      else if(args.length == 1) {
         try {
            parser = new HOPE2(new FileInputStream(args[0]));
            irFileName = args[0] + ".ll";
         }
         catch(FileNotFoundException e) {
            System.err.println("File " + args[0] + " not found");
            return;
         }
      }
      else {
         System.out.println("You must use the following format:");
         System.out.println("java HOPE2 inputfile");
      }

      try{
         SimpleNode root = parser.program();

         System.out.println("Abstract Syntax Tree:");
         root.dump(">");

         System.out.println("\nProgram:");
         PrintVisitor pv = new PrintVisitor();
         root.jjtAccept(pv, "");

         System.out.println("\nSymbol Table:");
         st.print();

         System.out.println("\nSemantic Analysis:");
         SemanticVisitor sv = new SemanticVisitor();
         root.jjtAccept(sv, st);

         System.out.println("\nWriting LLVM code to: " + irFileName);

         try {
            Context c = new Context(new BufferedWriter(new FileWriter(irFileName)));
            IrCodeVisitor irCode = new IrCodeVisitor();
            root.jjtAccept(irCode, c);
            c.buffer.flush();
         }
         catch (IOException e) {
            System.out.println("Failed to write LLVM code to file");
            e.printStackTrace(System.out);
         }

      }
      catch(ParseException e) {
         System.out.println(e.getMessage());
      }
   }

}

PARSER_END(HOPE2)

/* Section 3: Token Definitions */
TOKEN_MGR_DECLS : { static int commentNesting = 0; }

/* Ignores whitespace characters */
SKIP : {
   " " | "\t" | "\n" | "\r" | "\f"
}

/* Comments */
SKIP : {
   "/*" { commentNesting++; } : IN_COMMENT |
   "//" : IN_SINGLE_LINE_COMMENT
}

<IN_COMMENT> SKIP: {
   "/*" { commentNesting++; } |
   "*/" { commentNesting--; if(commentNesting == 0) { SwitchTo(DEFAULT); } } |
   <~[]>
}

<IN_SINGLE_LINE_COMMENT> SKIP: {
   <"\n" | "\r" | "\r\n"> : DEFAULT |
   <~[]>
}

TOKEN [IGNORE_CASE] : /* Keywords */
{
    <PRINT : "print"> |
    <INT : "int">
}

TOKEN : /* Punctuation */
{
   <LBR : "("> |
   <RBR : ")"> |
   <SEMIC : ";"> |
   <PLUS_SIGN : "+"> |
   <MINUS_SIGN : "-"> |
   <MUL_SIGN : "*"> |
   <DIV_SIGN : "/"> |
   <EQUALS : "=">
}

TOKEN : /* Numbers and identifiers */
{
   <NUM : ("0" | <INTEGER_LITERAL>)> |
   <#INTEGER_LITERAL : ["1"-"9"](["0"-"9"])*> |
   <ID : <LETTER>(<LETTER> | <INTEGER_LITERAL> | "_")*> |
   <#LETTER : ["A"-"Z","a"-"z"]>
}

TOKEN : /* Unrecognized tokens */
{
    <OTHER : ~[]>
}

/* Section 4: Grammar */

SimpleNode program() : {}
{
   statement_block() <EOF>
   { return jjtThis; }
}

void statement_block() : {}
{
   statement() statement_block() | {}
}

void statement() : {}
{
   print() |
   assignment() |
   declaration()
}

void expression() #void : {}
{
   fragment() (binary_arith_op() fragment() #expression(3))*
}

void fragment() #void : {}
{
   integer() | rhs_identifier()
}

void integer() : { Token t; }
{
   ( t = <NUM> )  { jjtThis.value = t.image; }
}

void assignment() : {}
{
   lhs_identifier() <EQUALS> expression() <SEMIC>
}

void declaration() : { String id; }
{
   type() id = lhs_identifier() <SEMIC> { st.insert(id); }
}

void print() : {}
{
   <PRINT> <LBR> expression() <RBR> <SEMIC>
}

String lhs_identifier() : { Token t; }
{
   ( t = <ID> ) { jjtThis.value = t.image; return t.image; }
}

String rhs_identifier() : { Token t; }
{
   ( t = <ID> ) { jjtThis.value = t.image; return t.image; }
}

String type() : { Token t; }
{
   ( t = <INT> ) { jjtThis.value = t.image; return t.image; }
}

void binary_arith_op() : { Token t; }
{
   ( t = <PLUS_SIGN> | t = <MINUS_SIGN> | t = <MUL_SIGN> | t = <DIV_SIGN> ) { jjtThis.value = t.image; }
}
