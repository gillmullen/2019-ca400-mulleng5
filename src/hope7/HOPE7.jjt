/* Section 1: Options */
options {
   MULTI = true;
   VISITOR = true;
   JAVA_UNICODE_ESCAPE = true;
}

/* Section 2: Driver */
PARSER_BEGIN(HOPE7)

import java.io.FileInputStream;
import java.io.IOException;
import java.io.FileNotFoundException;
import java.io.BufferedWriter;
import java.io.FileWriter;
import java.util.ArrayList;

public class HOPE7 {

   public static SymbolTable st = new SymbolTable();
   public static String scope = "global";

   public static void main(String args[]) {
      HOPE7 parser = null;
      String irFileName = "HOPE7.ll";

      if(args.length == 0) {
         System.out.println("Reading input...");
         parser = new HOPE7(System.in);
      }
      else if(args.length == 1) {
         try {
            parser = new HOPE7(new FileInputStream(args[0]));
            irFileName = args[0] + ".ll";
         }
         catch(FileNotFoundException e) {
            System.err.println("File " + args[0] + " not found");
            return;
         }
      }
      else {
         System.out.println("You must use the following format:");
         System.out.println("java HOPE7 inputfile");
      }

      try{
         SimpleNode root = parser.program();

         System.out.println("Abstract Syntax Tree:");
         root.dump(">");

         System.out.println("\nProgram:");
         PrintVisitor pv = new PrintVisitor();
         root.jjtAccept(pv, "");

         System.out.println("\nSymbol Table:");
         st.print();

         System.out.println("Semantic Analysis:");
         SemanticVisitor sv = new SemanticVisitor();
         root.jjtAccept(sv, st);

         System.out.println("\nWriting LLVM code to: " + irFileName);

         try {
            Context context = new Context (new BufferedWriter (new FileWriter (irFileName)), new ArrayList<DeclaredStrings> ());
            IrCodeVisitor irCode = new IrCodeVisitor();
            root.jjtAccept(irCode, context);
            context.buffer.flush();
         }
         catch (IOException e) {
            System.out.println("Failed to write LLVM code to file");
            e.printStackTrace(System.out);
         }
      }
      catch(ParseException e) {
         System.out.println(e.getMessage());
      }
   }
}

PARSER_END(HOPE7)

/* Section 3: Token Definitions */
TOKEN_MGR_DECLS : { static int commentNesting = 0; }

/* Ignores whitespace characters */
SKIP : {
   " " | "\t" | "\n" | "\r" | "\f"
}

/* Comments */
SKIP : {
   "/*" { commentNesting++; } : IN_COMMENT |
   "//" : IN_SINGLE_LINE_COMMENT
}

<IN_COMMENT> SKIP: {
   "/*" { commentNesting++; } |
   "*/" { commentNesting--; if(commentNesting == 0) { SwitchTo(DEFAULT); } } |
   <~[]>
}

<IN_SINGLE_LINE_COMMENT> SKIP: {
   <"\n" | "\r" | "\r\n"> : DEFAULT |
   <~[]>
}

TOKEN [IGNORE_CASE] : /* Keywords */
{
    <PRINT : "print"> |
    <INT : "int"> |
    <STRING : "string"> |
    <BOOL : "boolean"> |
    <TRUE : "true"> |
    <FALSE : "false"> |
    <IF : "if"> |
    <ELSE : "else"> |
    <WHILE: "while"> |
    <DEF : "def"> |
    <MAIN : "main"> |
    <RETURN : "return">
}

TOKEN : /* Punctuation */
{
   <LBR : "("> |
   <RBR : ")"> |
   <SEMIC : ";"> |
   <PLUS_SIGN : "+"> |
   <MINUS_SIGN : "-"> |
   <MUL_SIGN : "*"> |
   <DIV_SIGN : "/"> |
   <EQUALS : "="> |
   <OR : "|"> |
   <AND : "&"> |
   <QUOTE : "\""> |
   <LCB : "{"> |
   <RCB : "}"> |
   <EQ : "=="> |
   <NEQ : "!="> |
   <GT : ">"> |
   <LT : "<"> |
   <GE: ">="> |
   <LE : "<="> |
   <COMMA : ","> |
   <LSB : "["> |
   <RSB : "]">
}

TOKEN : /* Numbers and identifiers */
{
   <NUM : ("0" | <INTEGER_LITERAL>)> |
   <#INTEGER_LITERAL : ["1"-"9"](["0"-"9"])*> |
   <ID : <LETTER>(<LETTER> | <INTEGER_LITERAL> | "_")*> |
   <STRING_LITERAL : "\""(<LETTER> | <NUM> | " " | "'" | "!")* "\""> |
   <#LETTER : ["A"-"Z","a"-"z"]>
}

TOKEN : /* Unrecognized tokens */
{
    <OTHER : ~[]>
}

/* Section 4: Grammar */
SimpleNode program() : {}
{
   function_declarations() main() <EOF>
   { return jjtThis; }
}

void main() : {}
{
   { scope = "main"; }
   ( <MAIN> <LCB> statement_block() <RCB> | statement_block() )
}

void function_declarations() : {}
{
   function() function_declarations() | {}
}

void function() : { String id; String retType; }
{
   <DEF> retType = type() id = lhs_identifier() { if(retType.equals("int")) { retType = "integer"; } st.insertFunc(scope, id, retType, "FUNC"); scope = id; }
   <LBR> parameter_list() <RBR> <LCB>
   statement_block()
   <RETURN> <LBR> ( expression() | {} ) <RBR> <SEMIC>
   <RCB>
}

void parameter_list() : {}
{
   parameter() ( <COMMA> parameter() )*
}

void parameter() : { String id; String type; }
{
   type = type() id = lhs_identifier() { if(type.equals("int")) { type = "integer"; } st.insert(scope, id, type, "PARAM"); }
}

void statement_block() : {}
{
   statement() statement_block() | {}
}

void statement() : {}
{
   LOOKAHEAD(3) function_call() |
   LOOKAHEAD(2) array_declaration() |
   print() |
   assignment() |
   declaration() |
   if_statement() |
   while_loop()
}

void assignment() : {}
{
   lhs_identifier() <EQUALS> expression() <SEMIC>
}

void declaration() : { String type; String id; }
{
   type = type() id = lhs_identifier() <SEMIC> { if(type.equals("int")) { type = "integer"; } st.insert(scope, id, type, "VAR"); }
}

void array_declaration() : { String type; String id; String size; }
{
   type = type() <LSB> size = array_size() <RSB> id = lhs_identifier() <SEMIC> { st.insertArray(scope, id, type, "VAR", size); }
}

String array_size() : { Token t; }
{
   ( t = <NUM> ) { jjtThis.value = t.image; return t.image; }
}

void print() : {}
{
   <PRINT> <LBR> expression() <RBR> <SEMIC>
}

void if_statement() : {}
{
   <IF> <LBR> condition() <RBR> <LCB> statement_block() <RCB> <ELSE> <LCB> statement_block() <RCB>
}

void while_loop() : {}
{
   <WHILE> <LBR> condition() <RBR> <LCB> statement_block() <RCB>
}

void function_call() : { String id; }
{
   id = rhs_identifier() { scope = id; } <LBR> argument_list() <RBR>
}

void argument_list() : {}
{
   argument() ( <COMMA> argument() )*
}

void argument() : {}
{
   fragment()
}

void expression() : {}
{
   LOOKAHEAD(3) function_call() | fragment() ( binary_arith_op() | binary_logic_op() )*
}

void condition() : {}
{
   fragment() ( comp_op() fragment() )*
}

void fragment() : {}
{
   integer() | string() | bool() | array() | rhs_identifier()
}

void integer() : { Token t; }
{
   ( t = <NUM> )  { jjtThis.value = t.image; }
}

void string() : { Token t; }
{
   ( t = <STRING_LITERAL> ) { jjtThis.value = t.image; }
}

void bool() : { Token t; }
{
   ( t = <TRUE> | t = <FALSE> ) { jjtThis.value = t.image; }
}

void array() : { }
{
   <LSB> fragment() ( <COMMA> fragment() )* <RSB>
}

String lhs_identifier() : { Token t; }
{
   ( t = <ID> ) { jjtThis.value = t.image; return t.image; }
}

String rhs_identifier() : { Token t; }
{
   ( t = <ID> ) { jjtThis.value = t.image; return t.image; }
}

String type() : { Token t; }
{
   ( t = <INT> | t = <STRING> | t = <BOOL> ) { jjtThis.value = t.image; return t.image; }
}

void binary_arith_op() : { Token t; }
{
   arith_op() fragment()
}

void arith_op() : { Token t; }
{
   ( t = <PLUS_SIGN> | t = <MINUS_SIGN> | t = <MUL_SIGN> | t = <DIV_SIGN> ) { jjtThis.value = t.image; }
}

void binary_logic_op() : {}
{
   logic_op() fragment()
}

void logic_op() : { Token t; }
{
   ( t = <AND> | t = <OR> ) { jjtThis.value = t.image; }
}

void comp_op() : { Token t; }
{
   ( t = <EQ> | t = <NEQ> | t = <GT> | t = <LT> | t = <GE> | t = <LE> ) { jjtThis.value = t.image; }
}
